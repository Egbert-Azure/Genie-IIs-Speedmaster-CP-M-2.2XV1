;********************************************************
;*							*
;*	LOADER.MAC  System Bootstrap Loader Module	*
;*	Genie IIs  CP/M 2.2XV3  Version 1.0  19.05.86	*
;*	Copyright (c) Klaus K{mpf Softwareentwicklung	*
;*					1984,1985,1986	*
;*	Modification History				*
;*	0.0  19.04.86  Original Genie III Version	*
;*	0.1  26.04.86  1st IIs Adaption			*
;*	0.11 28.04.86  further IIs Adaption		*
;*      0.2  30.04.86  Graphic 80x24 Adaption		*
;*	0.21  1.05.86  Charset loading corrected	*
;*	0.22  4.05.86  new phase CE00 for extended	*
;*		       terminal support			*
;*	0.3  15.05.86  Added 2nd charset		*
;*		       removed clock			*
;*	0.4  18.05.86  SIO Adaption			*
;*		       new phase D200			*
;*	1.0  19.05.86  1st final Version		*
;*							*
;********************************************************
;
	.z80
	.com
	org	100h
	.phase 0D200h
;
back	equ	08h
lf	equ	0ah
cr	equ	0dh
esc	equ	1bh
;
start:
	jp	entry

	defb	0dh
	defm	'LOADER.SYS (c) Klaus K{mpf Softwareentwicklung 1986'
	defb	cr,lf
	defm	'Genie IIs CP/M 2.2XV1 Version 0.4 vom 18.05.86'
quit_text:
	defb	1ah
	defb	0
entry:
	ld	sp,stack
	ld	a,'L'
	ld	(3c00h),a
	call	mbios				;init bios, copy dpb,xltab

	ld	de,start_text
	ld	c,1
	call	mdos				;print text

	ld	e,0
	ld	c,2
	call	mdos				;select drive
;----------------------------------------------------------
	ld	de,fcb_system
	ld	c,3
	call	mdos				;open bios22x.sys
	cp	1
	jp	z,no_cpm

	ld	de,bios_table
	ld	c,5
	call	mdos				;set dma
	ld	de,fcb_system
	ld	c,4
	call	mdos				;read 1st record (bios_table)
	or	a
	jp	nz,cpm_error

	ld	hl,(bios_table+44)		;get ?video address
	ld	(?cbios_video),hl

	ld	hl,(bios_table+0)		;get load address
	ld	(?cbios),hl
						;read rest of BIOS
read_bios_loop:
	ex	de,hl
	push	de
	ld	c,5
	call	mdos				;set dma
	ld	de,fcb_system
	ld	c,4
	call	mdos				;read
	pop	hl
	ld	de,128
	add	hl,de
	or	a
	jr	z,read_bios_loop
	cp	3
	jp	nz,cpm_error
;----------------------------------------------------------
	ld	de,fcb_config
	ld	c,3
	call	mdos				;open config
	cp	1
	jp	z,no_config

	ld	hl,systab_table			;to systab_table
	ld	b,2				;2 records
	call	read_config

	ld	hl,(bios_table+4)		;to fkytab
	ld	b,2				;2 records
	call	read_config

	ld	hl,(bios_table+6)		;to dpb
	push	hl
	ld	b,1				;1 record
	call	read_config
	ld	hl,xltab_table			;to xltab_table
	ld	b,2				;2 records
	call	read_config

	call	disable_memio

	ld	hl,42a0h
	ld	de,xltab_table
	ld	bc,64
	ldir

	pop	de
	push	de				;de = dpbtab
	inc	hl				;hl = 42e1h
	ld	bc,31
	ldir					;move dpb
	pop	de

	ld	hl,4*32
	add	hl,de
	ex	de,hl				;de -> xltab(s) (52)
	ld	hl,xltab_table			;hl -> xltab_table (64)
	ld	b,4				;4 xltabs
move_xltab_loop:
	push	bc
	ld	bc,52
	ldir
	ld	bc,12
	add	hl,bc
	pop	bc
	djnz	move_xltab_loop

	call	enable_memio

	ld	b,6				;6 records
	ld	hl,(bios_table+40)		;to charset
	call	read_config
	ld	b,1
	ld	hl,xltab_table			;unused now !
	call	read_config			;read 2nd charset
	ld	hl,(bios_table+40)
	ld	de,6*128
	add	hl,de
	ld	de,xltab_table
	ex	de,hl
	ld	bc,64
	ldir
;
; ^ Read Bios & Config
;----------------------------------------------------------
; v Setup

	call	disable_memio

	ld	hl,systab_table+20h
	ld	de,(bios_table+2)		;de=systab start
	push	de
	ld	bc,(bios_table+28)		;bc=systab length
	ldir

	pop	de
	ld	hl,12h
	add	hl,de
	ld	(hl),0				;CHARSET GERMAN	!!

	ld	hl,systab_table+2		;hl=00/c9
	ld	de,bios_table+10		;de=pointer
	ld	bc,08ffh			;b=8 for djnz, c=ff for ldi
ctrl_config_loop:
	push	de
	ex	de,hl
	ld	a,(hl)
	inc	hl
	ld	h,(hl)
	ld	l,a
	ex	de,hl
	ldi					;ld (de),(hl)
	pop	de
	inc	de
	inc	de
	djnz	ctrl_config_loop

	ld	iy,systab_table			;iy -> systab_table

	call	sio_init
	
	ld	hl,systab_table+80h		;hl = auto command
	ld	de,(bios_table+8)		;de = keyboard buffer
	ld	b,0
set_auto_loop:					;set auto cmd
	ld	a,(hl)
	or	a
	jr	z,end_of_auto
	ld	(de),a
	inc	hl
	inc	de
	inc	b
	jr	set_auto_loop
end_of_auto:
	ld	ix,(bios_table+2)
	ld	(ix+8),b			;set inlen

	ld	a,(iy+00h)			;set iobyte
	ld	(3),a

	xor	a				;set boot disk to A:
	ld	(4),a

	ld	a,(iy+0ah)			;Ints enabled ?
	or	a
	jr	nz,no_ints			;N: jp
	ld	hl,(bios_table+1ah)		;Y: set <EI>
	ld	(hl),<ei>
	ld	hl,(bios_table+22h)		;HL -> kbstat
	ld	de,(bios_table+24h)		;DE = inkbds
	ld	(hl),e
	inc	hl
	ld	(hl),d
no_ints:

	ld	hl,systab_table+0a0h		;set ctrl key table
	ld	de,(bios_table+26h)
	ld	bc,16
	ldir

	ld	de,quit_text
	ld	c,1
	call	mdos				;print text

	ld	a,(iy+6ah)			;set cursor
	add	a,'0'
	ld	(esc_text+2),a
	ld	a,(iy+32h)			;set charset
	add	a,'2'
	ld	(esc_text+5),a
	ld	hl,esc_text
	call	print

	ld	a,(iy+1)			;Logo on ?
	or	a
	ld	hl,start_text
	call	z,print

	ld	hl,systab_table+0b0h		;Print Info text
	call	print

	jp	<?cbios: dw 0>
;----------------------------------------------------------
;
enable_memio:
	ld	a,0000_0110b
switch_around:
	out	(0feh),a
	ret
disable_memio:
	ld	a,0000_0111b
	jr	switch_around
;
print:
	ld	a,(hl)
	or	a
	ret	z
	push	hl
	ld	c,a
	call	<?cbios_video: dw 0>
	pop	hl
	inc	hl
	jr	print
;
sio_init:
	ld	hl,(bios_table+46)		;hl -> @siotab
	ld	de,systab_table+75h		;de -> sio table+5
	inc	hl				;hl -> @siotab+1
	ld	bc,3
	push	bc
	ldir
	inc	hl				;hl -> @siotab+5
	ld	de,systab_table+7dh
	pop	bc
	ldir
	ld	hl,systab_table+70h
	ld	a,(hl)
	out	(0f1h),a			;Set baudrate
	ld	a,(systab_table+78h)
	out	(0f2h),a			;Set BTx
	ld	c,0d2h				;SIO A first
	call	sio_init_1
	inc	hl
	inc	hl
	inc	c
sio_init_1:
	ld	b,5
	xor	a
	inc	hl
sio_init_loop:
	inc	a
	out	(c),a
	outi
	jr	nz,sio_init_loop
	ret
;
;----------------------------------------------------------
read_config:
	ex	de,hl
	push	bc
	push	de
	ld	c,5
	call	mdos				;set dma
	ld	de,fcb_config
	ld	c,4
	call	mdos				;read
	pop	hl
	ld	de,128
	add	hl,de
	pop	bc
	or	a
	jp	nz,sys_error
	djnz	read_config

	ret
;----------------------------------------------------------
cpm_error:
sys_error:
	ld	de,txt_sys_error
	jr	error
no_config:
	ld	de,txt_no_config
	jr	error
no_cpm:
	ld	de,txt_no_cpm
error:
	ld	c,1
	call	mdos			;show error text
	jp	$
;
start_text:
	defb	1ah
	defm	'GENIE IIs  CP/M 2.2X Version 1'
	defb	0dh,0ah,0
esc_text:
	defb	1bh,'.0'		;cursor
	defb	1bh,'_2'		;charset
	defb	0
txt_sys_error:
	defm	'Err: '
	defb	0
txt_no_config:
	defm	'Kein CONFIG.SYS'
	defb	0
txt_no_cpm:
	defm	'Kein BIOS22X.SYS'
	defb	0
;
fcb_config:
	defb	0
	defm	'CONFIG  '
	defm	'SYS'
	defs	35,0
fcb_system:
	defb	0
	defm	'BIOS22X '
	defm	'SYS'
	defs	35,0

	include MDOS.MAC
	include MBIOS.MAC


bios_table	equ	$
systab_table	equ	$+128
xltab_table	equ	$+128+256
stack		equ	$+128+256+256+64	;32 level stack

	if2
	.printl End of Loader
	.printn $
	.printl Max used address
	.printn $+128+256+256+64
	endif

	.dephase
	end
