;****************************************************************
;*								*
;*	IOBYTE.MAC  Shows decimal IOBYTE-Value			*
;*	Genie IIs/III/IIIs CP/M 2.2 Version 1.1 vom 19.5.86	*
;*	Copyright (c) by Klaus K{mpf Softwareentwicklung 1986	*
;*								*
;****************************************************************
;
	.z80
	.com
	org	100h
;
	include CPMHEAD.INC
;
entry:
	jp	start

	defb	cr
	defm	'IOBYTE.COM'
	defb	cr,lf
	defm	'Copyright (c) by Klaus K{mpf Softwareentwicklung 1986'
	defb	cr,lf
	defm	'CP/M 2.2  Version 1.1 vom 19.05.86'
	defb	cr,lf,ctrlz
;
start:
	ld	ix,flag
	ld	hl,(@iobyte)
	ld	h,0
	ld	de,number
	call	dez_out
	ld	de,text
	ld	c,9
	jp	?bdos
;
flag:	defb	0
;
dez_out:
	ld	bc,100
	call	div
	call	asctst
	ld	bc,10
	call	div
	call	asctst
	ld	bc,1
	call	div
	call	asct2
	ret
;
div:				; teilt hl/bc und gibt Erg als Dez-Zahl
	xor	a
div1:
	or	a
	sbc	hl,bc
	jr	c,div2
	inc	a
	jr	div1
div2:
	add	hl,bc
	ret
;
asctst:
	or	a
	jr	nz,asct1
	bit	0,(ix)
	jr	nz,asct2
	ld	a,space-'0'
	jr	asct2
asct1:
	set	0,(ix)
asct2:
	add	a,'0'
	ld	(de),a
	inc	de
	ret
;
text:
	defb	cr,lf
	defm	'Der Wert des IOBYTE ist: '
number:
	defm	'   '
	defb	cr,lf,'$'

	end
