;****************************************************************
;*								*
;*	CPMTOAP3.MAC  CP/M  ->  Alphatronic P3			*
;*	CP/M 2.2  Version 1.1 vom  01.08.86			*
;*	Copyright (c) by Klaus K{mpf Softwareentwicklung	*
;*						1985, 1986	*
;*	Modification History					*
;*	1.0  21.12.85  Original Version				*
;*	1.1  01.08.86  this header added			*
;*		       converted to standard CP/M		*
;*	1.2  25.08.86  version for old 2.2X			* 
;*								*
;****************************************************************
;
	.z80
	.com
	org	100h

	include BIOSHED.LIB
	include CPMHEAD.LIB
;
#get_track	equ	2
#put_track	equ	1
#sectors	equ	40
;
;----------------------------------------------------------
	jp	entry

	defb	cr
	defm	'CPMTOAP3.COM'
	defb	cr,lf
	defm	'Copyright (c) by Klaus K{mpf Softwareentwicklung 1985,1986'
	defb	ctrlz
;
entry:
	print	heading

	ld	hl,(?boot+1)			;HL -> ?bios+3
	ld	l,0
	ld	(?@bios),hl

	ld	a,(@fcb)
	or	a				;Keine Disk angegeben ?
	jp	z,no_disk_given			;-> Fehler
	dec	a
	ld	c,a
	ld	e,0
	bios_call	b_seldsk		;Select Disk
	ld	a,h
	or	l
	jp	z,no_disk_found

	print	txt_read_track
	ld	bc,#get_track			;Get Track
	ld	(@track),bc
	call	read
	print	txt_write_track
	ld	bc,#put_track			;Write Track
	ld	(@track),bc
	call	write
	print	txt_good_exit
	jp	?boot
;
;
;
read:
	ld	b,#sectors
	ld	hl,track_buffer
	ld	de,128
read_loop:
	push	bc,de,hl
	ld	(@read_dma),hl
	ld	a,#sectors+8
	sub	b
	ld	c,a
	ld	b,0
	bios_call	b_setsec
	ld	bc,(@track)
	bios_call	b_settrk
	ld	bc,<@read_dma: dw 0>
	bios_call	b_setdma
	bios_call	b_read
	or	a
	jp	nz,read_error
	pop	hl,de,bc
	add	hl,de
	djnz	read_loop

	ret
;
;
;
write:
	ld	b,#sectors
	ld	hl,track_buffer
	ld	de,128
write_loop:
	push	bc,de,hl
	ld	(@write_dma),hl
	ld	a,#sectors+8
	sub	b
	ld	c,a
	ld	b,0
	bios_call	b_setsec
	ld	bc,(@track)
	bios_call	b_settrk
	ld	bc,<@write_dma: dw 0>
	bios_call	b_setdma
	ld	c,1				;make it a DIR write
	bios_call	b_write
	or	a
	jp	nz,write_error
	pop	hl,de,bc
	add	hl,de
	djnz	write_loop

	ret
;
;
no_disk_given:
	print	txt_no_disk_given
	jr	err_exit
no_disk_found:
	print	txt_no_disk_found
	jr	err_exit
read_error:
	print	txt_read_error
	jr	err_exit
write_error:
	print	txt_write_error
err_exit:
	jp	?boot

;
?bios_call:
	ld	hl,<?@bios: dw 0>
	ld	l,a
	jp	(hl)
;
reset_system:
	bdos_call	13
	ret
;
;----------------------------------------------------------
;
heading:
	defb	cr,lf
	defm	'Disketten-Konvertierung  CP/M => Alphatronic P3'
	defb	cr,lf
	defm	'Copyright (c) by Klaus K{mpf Softwareentwicklung 1986'
	defb	cr,lf,cr,lf,'$'
txt_no_disk_given:
	defb	bell,cr,lf
	defm	'Keine Laufwerksbezeichnung angegeben !'
	defb	cr,lf
	defm	'Bitte mit CPMTOAP3 X: neu starten, wobei X: der Laufwerks'
	defm	'namen ist.$'
txt_no_disk_found:
	defb	bell,cr,lf
	defm	'Das angegebene Laufwerk kann nicht angew{hlt werden !'
	defb	cr,lf
	defm	'Entweder ist das Laufwerk gesperrt oder es enth{lt keine '
	defm	'Diskette.$'
txt_read_track:
	defm	'Lese Track '
	defb	#get_track+'0'
	defm	' von Alphatronic P3 Diskette.'
	defb	cr,lf,'$'
txt_read_error:
	defb	bell,cr,lf
	defm	'Beim Lesen ist ein Fehler aufgetreten.'
	defb	cr,lf
	defm	'Hat das Laufwerk das Format ALPHATRONIC P3 zugewiesen '
	defm	'bekommen ?$'
txt_write_track:
	defm	'Schreibe Track '
	defb	#put_track+'0'
	defm	'auf Alphatronic P3 Diskette.'
	defb	cr,lf,'$'
txt_write_error:
	defb	bell,cr,lf
	defm	'Beim Schreiben ist ein Fehler aufgetreten,'
	defb	cr,lf
	defm	'Ist die Diskette schreibgesch}tzt ?$'
txt_good_exit:
	defm	'Umkopierung erfolgreich beendet.$'
;
@track:		defw	0
;
track_buffer:
	defs	#sectors*128,0

	end
