; Boot Sector zum CP/M 2.2X CBIOS
; (c),(p) Klaus K{mpf 1984
;
ccp     equ  0d100h

        org  4200h

entry:  di
        ld   sp,0
        ld   a,5
        ld   (errcnt),a
        xor  a
        ld   bc,10f7h          ;Init CRTC
        ld   hl,crtab
crloop: out  (0f6h),a
        inc  a
        outi
        jr   nz,crloop

        ld   a,1               ;Invers
        out  (0f5h),a
        ld   a,75h             ;swtchi
        out  (0fah),a

        ld   a,(3840h)
        and  80h               ;Space ?
        jr   z,notspc
        ld   a,0c9h            ;Y: Dont setcmd
notspc: ld   (setcmd),a
        ld   hl,37ech
        ld   (hl),0ffh         ;DDEN
        ld   (hl),0d0h
        ld   a,01h             ;Drive A:
        ld   (37e0h),a
        ld   (hl),03h          ;Restore
        call delay
        ld   (hl),53h          ;Step IN
        call delay
        ld   de,ccp            ;CCP-Start
        ld   bc,0501h          ;5 Sectors on Front
        call read
        ld   bc,0511h          ;5 Sectors on Back
        call read

        ld   a,07fh            ;swtcho
        out  (0fah),a

        call setcmd

        jp   ccp+1600h         ;BIOS Cboot Vector

setcmd: nop
        ld   de,ccp+07h
        ld   hl,cmd
        ld   bc,14             ;8 Chars
        ldir
        ret

delay:  ld   b,20h
        djnz $
wait:   ld   a,(hl)
        rrca
        ret  nc
        jr   wait

resel:  defb 3eh               ;'LD A,'
selcod: defb 11h
        ld   (37e1h),a
        ret

read:   ld   a,c
        ld   (selcod),a
        ld   c,0
read1:  call resel
        ld   a,c
        ld   (37eeh),a         ;Set sector
        ld   (hl),88h
        ld   a,20h
read2:  dec  a
        jr   nz,read2
nodrq:  bit  1,(hl)
        jr   nz,drq
        bit  1,(hl)
        jr   nz,drq
        bit  0,(hl)
        jr   z,nobusy
        bit  1,(hl)
        jr   z,nodrq
drq:    ld   a,(37efh)         ;Get data
        ld   (de),a
        inc  de
        jr   nodrq

nobusy: ld   a,(hl)
        ld   (hl),0d0h
        and  1ch
        jr   nz,retry
        call print
        ld   a,05
        ld   (errcnt),a
        inc  c
        djnz read1
        ret

retry:  ld   a,(errcnt)
        dec  a
        jp   z,entry
        ld   (errcnt),a
        jr   read1

print:  push hl
        ld   hl,(prtpos)
        ld   a,l
        add  a,30h
        ld   (hl),a
        inc  hl
        ld   (prtpos),hl
        pop  hl
        ret

cmd:    defb 12
        defm 'SUBMIT START'
        defb 0

crtab:  defb 66h,50h,56h,34h,1ah,00h,19h,1ah
        defb 00h,0bh,0bh,0bh,00h,00h,00h,00h

errcnt: defb 0

prtpos: defw 3c00h

        defm '(c,p) Klaus K{mpf'

zzzz    equ  $

        end
