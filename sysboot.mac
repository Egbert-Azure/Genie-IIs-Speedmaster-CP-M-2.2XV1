	.z80
	.com
	org	0100h
;****************************************************************
;*								*
;*	SYSBOOTX.MAC  Boot Sector Module			*
;*	GENIE IIs  CP/M 2.2XV1 Version 1.0  19.05.86		*
;*	Copyright (c),(p) by Klaus K{mpf Softwareentwicklung	*
;*						1984,1985,1986	*
;*	Modification History					*
;*	0.0  xx.09.85  Original Genie III Version		*
;*	0.1  26.04.86  1st Speedmaster/IIs Adaption		*
;*	0.11 27.04.86  Changed Side-Select to Bit 3		*
;*	0.12 28.04.86  Changed Side-Select back to 4		*
;*	0.13  5.05.86  new loader phase CE00H			*
;*	0.14 15.05.86  set port 0f9h, im 1			*
;*	0.2  18.05.86  new loader phase D200H			*
;*	1.0  19.05.86  1st final Version			*
;*								*
;****************************************************************
;
loader		equ	0d200h		;Loader address
max_error	equ	5		;Max # of retries
#side		equ	0001_0000b
	.phase  4200h			;Boot address (by EPROM)

entry:
	di
	ld	sp,stack
	xor	a
	out	(0f9h),a
	im	1
	ld	a,0000_0100b			;swtchi
	out	(0feh),a
	ld	a,0feh
	ld	(37ech),a
	ld	a,080h
	ld	(37eeh),a
	ld	a,0d0h
	ld	(37ech),a

	ld	a,'B'				;'BOOTER'
	ld	(3c00h),a

	ld	hl,37ech
	ld	de,loader			;Loader-Start
	push	de				;set as return adr
;
	ld	a,01h				;Front, drive 0
	ld	bc,0901h			;9 Sectors from 1 up
	call	read
	ld	a,01h+#side			;Back, drive 0
	ld	bc,0700h			;7 Sectors from 0 up
						;fall through to read
						;return to loader
;----------------------------------------------------------
read:
	ld	(selcod),a
read1:
	call	resel
	ld	a,c
	ld	(37eeh),a	 		;Set sector
	push	bc
	ld	bc,37efh			;BC -> data
	call	resel
	ld	(hl),88h
	ld	a,10
wait:
	dec	a
	jr	nz,wait
	bit	0,(hl)
	jr	z,nobusy
nodrq:
	bit	1,(hl)
	jr	nz,drq
	bit	1,(hl)
	jr	nz,drq
	bit	0,(hl)
	jr	z,nobusy
	bit	1,(hl)
	jr	z,nodrq
drq:
	ld	a,(bc)	 			;Get data
	ld	(de),a
	inc	de
	jp	nodrq
nobusy:
	ld	a,(hl)
	pop	bc
	ld	(hl),0d0h
	and	0001_1100b
	jr	nz,error
	inc	c
	djnz	read1
	ret
;
error:
	rrca
	rrca
	add	a,'0'
	ld	(3c01h),a
	ld	a,'E'
	ld	(3c00h),a
	jr	$
;
resel:
	LD	A,<selcod: 01h>
	ld	(37e1h),a
	ret
;
	defs	8,0
stack:
zzzz    equ  $
	.printn zzzz

	defs	4300h-$,0

	.dephase

	end
